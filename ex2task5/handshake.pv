type key.

fun senc(bitstring, key): bitstring.

reduc forall m: bitstring, k: key; sdec(senc(m, k), k) = m.

type skey.
type pkey.

fun pk(skey): pkey.
fun aenc(bitstring, pkey): bitstring.

reduc forall m: bitstring, k: skey; adec(aenc(m, pk(k)), k) = m.

type nonce. (*new type*)

type sskey.
type spkey.

fun spk(sskey): spkey.
fun sign(bitstring, sskey): bitstring.

reduc forall m: bitstring, k: sskey; getmess(sign(m, k)) = m.
reduc forall m: bitstring, k: sskey; checksign(sign(m, k), spk(k)) = m.

free c: channel.
free s: bitstring [ private ].

event aliceReceiveKey(key).
event bobFreshKey(key).

event aliceAccepts(key).
event bobAccepts(key, pkey).
event aliceTerm(key, pkey).
event bobTerm(key).

query attacker(s).
query x: key; event(aliceReceiveKey(x)) ==> event(bobFreshKey(x)).

query skx: key, pky: pkey; inj-event(aliceTerm(skx,pky)) ==> inj-event(bobAccepts(skx,pky)).

(* The above does not hold for injective version inj-event, meaning
   that the replay attack on Alice is possible. Fix the protocol so that the 
   above query verifies also with inj-event. *)

(*Fixed*)

query skx: key; inj-event(bobTerm(skx)) ==> inj-event(aliceAccepts(skx)).

let alice(pkA: pkey, skA: skey, pkB: spkey) =
  (*Send a nonce to Bob*)
  new noA: nonce;
  out(c, noA);

  out(c, pkA);
  in(c, x: bitstring);
  let sigB = adec(x, skA) in
  let (=pkA, =pkB, k: key, noB: nonce) = checksign(sigB, pkB) in
  event aliceReceiveKey(k);
  event aliceAccepts(k);
  if noA = noB then out(c, senc(s, k)); (*Check if recieved nonce is same as created*)
  event aliceTerm(k, pkA).

let bob(pkB: spkey, skB: sskey, pkA: pkey) =
  in(c, noB: nonce); (*Recieved nonce*)
  
  in(c, pkX: pkey);
  new k: key;
  event bobFreshKey(k);
  event bobAccepts(k, pkX);
  out(c, aenc(sign((pkX, pkB, k, noB), skB), pkX)); (*Add recieved nonce to signed message*)
  in(c, x:bitstring);
  let z = sdec(x, k) in 
  if pkX = pkA then event bobTerm(k).

process
  new skA: skey; new skB: sskey;
  let pkA = pk(skA) in out(c, pkA);
  let pkB = spk(skB) in out(c, pkB);
  ( (!alice(pkA, skA, pkB)) | (!bob(pkB, skB, pkA)) )

